apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "0.9.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a" // DO NOT MODIFY THIS LINE, IT'S UPDATED BY BUILD MACHINE AUTOMATICALLY.
        }
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    aaptOptions {
        noCompress "tflite"
        noCompress "lite"
    }

    libraryVariants.all { variant ->
        variant.assemble.doLast {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null) {
                    def fileName = "${project.name}${variant.flavorName}_${android.defaultConfig.versionName}"
                    def outputPath = "../app/libs"
                    copy {
                        from outputFile
                        into outputPath
                        rename { fileName + ".aar" }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:25.2.0'
    testImplementation 'junit:junit:4.12'

    implementation project(":AgoraRTCLib")
    implementation project(':LangGPUImageLib')
    implementation project(':SenseMeLibrary')
    implementation project(':yfGraphicBuffer')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}

